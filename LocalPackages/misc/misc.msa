*:/whoami [$] = >>>
  msg(player())
<<<

*:/whereami [$] = >>>
  msg('The Obsidian')
<<<

obsidian.admin:/signtext [$line] [$] = >>>
  if($line == ''){
    msg(color(red).'Usage: /signtext $line $text')
    msg('Supported color codes:')
    msg(colorize('&0&&0 &1&&1 &2&&2 &3&&3 &4&&4 &5&&5 &6&&6 &7&&7'))
    msg(colorize('&8&&8 &9&&9 &a&&a &b&&b &c&&c &d&&d &e&&e &f&&f'))
    msg('Typing && twice gives a single, literal &')
    die()
  }
  if($line < 1 || $line > 4){
    die(color(red).'The line number must be between 1 and 4!')
  }
  if(is_sign_at(pcursor())){
    @array = get_sign_text(pcursor())
    @array[$line - 1] = colorize($)
    set_sign_text(pcursor(), @array)
  } else {
    die('You aren\'t looking at a sign!')
  }
<<<

mods:/kick $player [$] = >>>
  # We can add people who shouldn't be kickable here.
  assign(@unkickable, array('jacobwg'))
  if(array_contains(@unkickable, player($player))){
    pkick(player(), 'NOPE')
  } else {
    run('/kick' $player $)
  }
<<<

*:/map [$] = >>>
  msg(color(GOLD).'You can find the map at '.color(CYAN).'http://map.the-obsidian.com')
<<<

*:/teamspeak [$] = >>>
  msg(color(GOLD).'Teamspeak Server: ts.the-obsidian.com (password: thetspassword)')
<<<

*:/ping = msg(concat(color(gray), 'Pong!'))

/each $ = >>>
  assign(@args, parse_args($))
  foreach(all_players(), @p, g(
    run(replace($, '^', @p))
  ))
<<<

/kickall $ = >>>
  foreach(all_players(), @p, g(
    pkick(@p, $)
  ))
<<<

/kickforrestart = call_alias('/kickall Server restarting.')
